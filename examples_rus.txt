это звезда
  повтори 5 [ вп 100 пр 144 ]
конец
сотри
звезда

это квадрат :длина
  повтори 4 [ вп :длина пр 90 ]
конец
это случцвет
  нов_цвет сл_элемент [ красный оранжевый желтый зеленый синий фиолетовый ]
конец
сотри
повтори 36 [ случцвет квадрат сл 200 пр 10 ]

сотри окно сч
повтори 144 [
  setlabelheight repcount
  пп
  вп repcount * repcount / 30
  label "Logo
  нд repcount * repcount / 30
  по
  пр 10
  жди 5
]
пч

это дерево :размер
   если :размер < 5 [вп :размер нд :размер стоп]
   вп :размер/3
   лв 30 дерево :размер*2/3 пр 30
   вп :размер/6
   пр 25 дерево :размер/2 лв 25
   вп :размер/3
   пр 25 дерево :размер/2 лв 25
   вп :размер/6
   нд :размер
конец
сотри
дерево 150

это папоротник :размер :знак
  если :размер < 1 [ стоп ]
  вп :размер
  пр 70 * :знак
    папоротник :размер * 0.5 :знак * -1 лв 70 * :знак
  вп :размер
  лв 70 * :знак
    папоротник :размер * 0.5 :знак пр 70 * :знак
  пр 7 * :знак
    папоротник :размер - 1 :знак лв 7 * :знак
  нд :размер * 2
конец
окно сотри пп нд 150 по
папоротник 25 1
